plugins {
    id 'java'
    id 'com.avast.gradle.docker-compose' version '0.7.1'
    id 'maven-publish'
}

group 'com.github.CST-Group'
version '0.1.6'

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    implementation group: 'org.apache.kafka', name: 'kafka_2.13', version: '2.7.0'
    implementation group: 'org.apache.logging.log4j', name:'log4j', version:'2.17.2'
    implementation group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.6.2'
    implementation group: 'com.github.CST-Group', name:'cst', version: '1.0.0'

    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.8.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'

}

dockerCompose {
    useComposeFiles = ['docker-compose.yaml']
}

test {
    if (project.hasProperty('integrationTest')) {
        dependsOn composeUp
        finalizedBy composeDown
    } else {
        exclude '**/**Integration**'
    }
}

clean {
    delete rootProject.buildDir
}

publishing {
    repositories {
        maven {
            name = "ctm"
            url = uri("https://maven.pkg.github.com/cst-group/ctm")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'com.github.CST-Group'
            artifactId = 'ctm'
            version = '0.1.6'

            from(components.java)
        }
    }
}
