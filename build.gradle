plugins {
    id 'java'
    id 'com.avast.gradle.docker-compose' version '0.7.1'
    id 'maven-publish'
}

group 'com.github.CST-Group'
version '1.0.0'
artifactId 'jdct'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    implementation group: 'org.apache.kafka', name: 'kafka_2.13', version: '2.7.0'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    implementation group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.6.2'
    implementation group: 'com.github.CST-Group', name:'cst', version: '0.6.1'

    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.8.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'

}

dockerCompose {
    useComposeFiles = ['docker-compose.yaml']
}

test {
    if (project.hasProperty('integrationTest')) {
        dependsOn composeUp
        finalizedBy composeDown
    } else {
        exclude '**/**Integration**'
    }
}

clean {
    delete rootProject.buildDir
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cst-group/jdct")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            groupId = 'com.github.CST-Group'
//            artifactId = 'JDCT'
//            version = version
//
//            from components.java
//        }
//    }
//}